plugins {
    id 'de.undercouch.download' version '3.1.1'
    id 'maven-publish'
}

import de.undercouch.gradle.tasks.download.Download

group 'com.jetbrains.jdk'

for (platform in ['osx', 'windows', 'linux']) {

    def capitalizedPlatform = platform.capitalize()
    def platformClassifier = "${platform}-x64"

    for (distroType in ['jbr', 'jbrsdk']) {

        def capitalizedDistroType = distroType.capitalize()

        def downloadTask = tasks.create(name: 'get' + capitalizedDistroType + capitalizedPlatform, type: Download) {
            def fileName = "${distroType}-${jdk_version}-${platformClassifier}-${jdk_build}.tar.gz"
            src "https://dl.bintray.com/jetbrains/intellij-jbr/$fileName"
            dest "$buildDir/$fileName"
            overwrite false
        }

        publishing {
            publications.create(capitalizedDistroType + capitalizedPlatform, MavenPublication) {
                artifact(downloadTask.dest) {
                    classifier platformClassifier
                    builtBy downloadTask
                    extension 'tgz'
                }
                artifactId distroType
                version "${jdk_version}-${jdk_build}"
            }
        }
    }
}

publishing {
    repositories {
        maven {
            if (project.hasProperty('nexusUsername')) {
                credentials {
                    username project.getProperty('nexusUsername')
                    password project.getProperty('nexusPassword')
                }
            }
            url project.findProperty('repository') ?: 'https://projects.itemis.de/nexus/content/repositories/mbeddr'
        }
        repositories {
            maven {
                name = "itemisCloud"
                url = project.version.contains("SNAPSHOT")
                        ? uri("https://artifacts.itemis.cloud/repository/maven-mps-snapshots/")
                        : uri("https://artifacts.itemis.cloud/repository/maven-mps-releases/")
                if (project.hasProperty("artifacts.itemis.cloud.user") && project.hasProperty("artifacts.itemis.cloud.pw")) {
                    credentials {
                        username = project.findProperty("artifacts.itemis.cloud.user")
                        password = project.findProperty("artifacts.itemis.cloud.pw")
                    }
                }
            }
           
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/mbeddr/build.publish.mps")
                if(project.hasProperty("gpr.token")) {
                    credentials {
                        username = project.findProperty("gpr.user")
                        password = project.findProperty("gpr.token")
                    }
                }
            }
        }
    }
}
